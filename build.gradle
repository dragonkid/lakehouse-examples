plugins {
    id "java"
    id "application"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "eclipse"
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

group "com.arecadata.clickstream"
version "0.0.1"
mainClassName = 'com.arecadata.clickstream.ClickStream'
description = "Streaming clickstream data into Iceberg from Kafka via Flink"

compileJava {
    options.release.set(11)
}

repositories {
    mavenCentral()
    maven {
        url "https://tabular-repository-public.s3.amazonaws.com/releases"
    }
    maven {
        url "https://repository.apache.org/content/repositories/snapshots"
    }
    maven {
        url "https://repo1.maven.org/maven2"
    }
}

ext {
    flinkVersion = "1.17.1"
    flinkShortVersion = "1.17"
    scalaVersion = "2.12"
    icebergVersion = "1.3.1"
    tabularVersion = "1.4.3"
    hadoopVersion = "3.3.2"
    paimonVersion = "0.4.0-incubating"
}

configurations {
    flinkShadowJar // dependencies to include in the shadow jar

    // provided by Flink...
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'org.apache.logging.log4j'
}

dependencies {
    flinkShadowJar "org.apache.iceberg:iceberg-flink-runtime-${flinkShortVersion}:${icebergVersion}"
    flinkShadowJar "org.apache.hadoop:hadoop-common:${hadoopVersion}" // needed by Iceberg Flink
    flinkShadowJar "io.tabular:tabular-client:${tabularVersion}"
    flinkShadowJar group: 'com.github.javafaker', name: 'javafaker', version: '1.0.2'
    flinkShadowJar "org.apache.flink:flink-table-api-java-bridge:${flinkVersion}"

    // these are provided by Flink...
    implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
    implementation "org.apache.flink:flink-table-planner_2.12:${flinkVersion}"
    implementation "org.apache.paimon:paimon-flink-${flinkShortVersion}:${paimonVersion}"
    implementation "org.apache.flink:flink-clients:${flinkVersion}"
    implementation "org.apache.flink:flink-table-runtime:${flinkVersion}"
    implementation "org.apache.iceberg:iceberg-flink-runtime-${flinkShortVersion}:${icebergVersion}"
    implementation "org.apache.hadoop:hadoop-common:${hadoopVersion}" // needed by Iceberg Flink
    implementation "io.tabular:tabular-client:${tabularVersion}"
    implementation group: 'com.github.javafaker', name: 'javafaker', version: '1.0.2'
    implementation "org.apache.hive:hive-metastore:3.1.3"
    implementation group: 'org.apache.hadoop', name: 'hadoop-mapred', version: '0.22.0'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation "org.apache.hadoop:hadoop-hdfs-client:${hadoopVersion}"
    implementation "org.apache.paimon:paimon-s3:${paimonVersion}"
    implementation "org.apache.flink:flink-connector-base:${flinkVersion}"
    implementation "org.apache.flink:flink-connector-files:${flinkVersion}"
}

sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar

    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar

    javadoc.classpath += configurations.flinkShadowJar
}

shadowJar {
    zip64 true
    configurations = [project.configurations.flinkShadowJar]
    mergeServiceFiles()
}
